plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.diffplug.spotless' version '8.1.0'
}

group = 'aegis'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

spotless {
    java {
        palantirJavaFormat()
        formatAnnotations()
        removeUnusedImports()
        importOrder(
                'java',
                'javax',
                'jakarta',
                '',
                'org.springframework',
                'org',
                'com',
                'lombok',
                '',
                'aegis',
                '\\#'
        )
        trimTrailingWhitespace()
        endWithNewline()
    }
}

tasks.named('compileJava') {
    dependsOn 'spotlessApply'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

def isMacOs = System.getProperty('os.name').toLowerCase().contains('mac')
def isArm64 = System.getProperty('os.arch').toLowerCase().contains('aarch64')

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-session-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'org.postgresql:postgresql'

    // Netty native DNS resolver for macOS ARM64
    // --enable-native-access=ALL-UNNAMED 옵션과 함께 실행해야 WARNING이 뜨지 않음 (25.12.31 기준)
    // IDEA Run/Debug Configurations의 VM options에 추가할 것
    if (isMacOs && isArm64) {
        runtimeOnly 'io.netty:netty-resolver-dns-native-macos:4.2.9.Final:osx-aarch_64'
    }

    implementation 'net.dv8tion:JDA:6.2.0'

    implementation 'com.google.api-client:google-api-client:2.8.1'
    implementation 'com.google.oauth-client:google-oauth-client-jetty:1.39.0'
    implementation 'com.google.apis:google-api-services-sheets:v4-rev20251110-2.0.0'

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.0'
    
    implementation 'com.google.zxing:core:3.5.4'
    implementation 'com.google.zxing:javase:3.5.4'

    testImplementation 'com.github.fppt:jedis-mock:1.1.12'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:testcontainers-junit-jupiter'
    testImplementation 'org.testcontainers:testcontainers-postgresql'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
}

test {
    useJUnitPlatform()
    jvmArgs += '-Xshare:off'
    jvmArgs += '-javaagent:' +
            configurations.testRuntimeClasspath.find { it.name.contains("byte-buddy-agent") }.absolutePath
}

// 버전 정보를 담은 파일을 생성하는 커스텀 태스크
abstract class WriteVersionFile extends DefaultTask {
    @Input
    abstract Property<String> getVersionText()

    @OutputFile
    abstract RegularFileProperty getOutputFile()

    @TaskAction
    void write() {
        def f = outputFile.get().asFile
        f.parentFile.mkdirs()
        f.text = versionText.get()
    }
}

tasks.register('writeVersionFile', WriteVersionFile) {
    versionText.set(providers.provider { version.toString() })
    outputFile.set(layout.buildDirectory.file('version.txt'))
}

jar.dependsOn writeVersionFile
